module io at "trilogy:io"
module num at "trilogy:num"

func chomp s <> "\n" = s
func chomp s = s

proc play!() {
    let answer = 42
    while true {
        io::print!("Guess: ")
        let guess = with io::readline!() |> chomp |> fn n. num::cast!(n)
            when 'NAN cancel {
                io::println!("That's not a valid number")
                continue
            }
            else yield
        if guess == answer {
            io::println!("Congratulations, that's right!")
            break
        } else if guess < answer {
            io::println!("Wrong, too low.")
        } else {
            io::println!("Wrong, too high.")
        }
    }
}

proc main!() {
    with play!()
        when 'EOF cancel { io::println!("Give up? Too bad.") }
        else cancel { exit 1 }
}
