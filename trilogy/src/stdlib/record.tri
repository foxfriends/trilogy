import "trilogy:core" as core
import "trilogy:array" as array

export contains_key, merge_by, collect

func update cmp key value record =
  if contains_key key record
    then {| ..record, key => cmp key value (record.key) |}
    else {| ..record, key => value |}

func merge_by merger lhs rhs =
  rhs
  |> core::to_array
  |> array::fold (fn state k:v. update merger k v state) lhs

test "record merge_by" {
  let max = fn key lhs rhs. if lhs > rhs then lhs else rhs
  assert merge_by max {| 'a => 1, 'b => 2, 'c => 3 |} {| 'a => 3, 'b => 1, 'c => 2 |} == {| 'a => 3, 'b => 2, 'c => 3 |}
}

func contains_key val record and typeof 'record = core::contains_key val record

test "record contains_key" {
  let record = {| 'a => 1, 'b => 2 |}
  assert contains_key 'a record
  assert contains_key 'b record
  assert !(contains_key 1 record)
}

func collect iterator =
  let record = {||},
  with { iterator!(); record } {
    when 'next(key:val) then {
      record.key = val
      become unit
    }
  }

test "record collect" {
  let iterator = do() {
    yield 'next(1: 'a)
    yield 'next(2: 'b)
    yield 'next(3: 'c)
    yield 'next(2: 'd)
    yield 'next(1: 'e)
  }
  assert collect iterator == {| 3 => 'c, 2 => 'd, 1 => 'e |}
}
