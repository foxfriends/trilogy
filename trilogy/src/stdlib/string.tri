import "trilogy:core" as core
import "trilogy:array" as array

export is_empty, chars, join, length, slice, starts_with, replace, replace_all

func is_empty "" = true
func is_empty _ = false

test "string is_empty" {
  assert (is_empty "")
  assert !(is_empty "hello")
}

func chars str and typeof 'string = core::to_array str

test "string chars" {
  assert chars "hello" == ['h', 'e', 'l', 'l', 'o']
  assert chars "" == []
}

func join _ [] = ""
func join _ [a] = $"${a}"
func join sep [a, ..strs] = $"${a}${sep}${join sep strs}"

test "string join" {
  assert join " " ["hello", "there", "world"] == "hello there world"
  assert join "" ["hello", "there", "world"] == "hellothereworld"
  assert join 'x' ["hello", "there", "world"] == "helloxtherexworld"
  assert join 5 [1, 2, 3] == "15253"
}

func length str and typeof 'string = core::length str

test "string length" {
  assert length "hello" == 5
  assert length "" == 0
}

func slice i fin (str and typeof 'string) = str
  |> chars
  |> array::slice i fin
  |> join ""

test "string slice" {
  assert slice 0 3 "123" == "123"
  assert slice 1 2 "123" == "2"
  assert slice 1 1 "123" == ""
  assert slice 1 3 "123" == "23"
  assert with (slice (-1) 2 "123"; false)
    when 'arg cancel true
    else yield
  assert with (slice 2 1 "123"; false)
    when 'arg cancel true
    else yield
  assert with (slice 0 5 "123"; false)
    when 'mia cancel true
    else yield
  assert with (slice 5 5 "123"; false)
    when 'mia cancel true
    else yield
}

func starts_with prefix string = slice 0 (length prefix) string == prefix

test "string starts_with" {
  assert starts_with "" ""
  assert starts_with "" "hello"
  assert starts_with "he" "hello"
  assert starts_with "hello" "hello"
  assert !(starts_with "be" "hello")
  assert !(starts_with "el" "hello")
}

func replace "" _ _ = yield 'arg
func replace _ _ "" = ""
func replace original replacement string =
  if length original > length string
    then string
  else if starts_with original string
    then replacement <> slice (length original) (length string) string
  else $"${string.0}" <> (replace original replacement <| slice 1 (length string) string)

test "string replace" {
  assert replace "h" "y" "hello" == "yello"
  assert replace "l" "n" "hello" == "henlo"
  assert replace "hel" "y" "hello" == "ylo"
  assert replace "lo" "mi" "hello" == "helmi"
  assert replace "elt" "mo" "hello" == "hello"
  assert replace "a" "b" "" == ""
}

func replace_all "" _ _ = yield 'arg
func replace_all _ _ "" = ""
func replace_all original replacement string =
  if length original > length string
    then string
  else if starts_with original string
    then replacement <> (replace_all original replacement <| slice (length original) (length string) string)
  else $"${string.0}" <> (replace_all original replacement <| slice 1 (length string) string)

test "string replace_all" {
  assert replace_all "h" "y" "hello" == "yello"
  assert replace_all "l" "n" "hello" == "henno"
  assert replace_all "hel" "y" "hello" == "ylo"
  assert replace_all "lo" "mi" "hello" == "helmi"
  assert replace_all "elt" "mo" "hello" == "hello"
  assert replace_all "a" "b" "" == ""
}
