#! Basic functions, helpers, and combinators for functional programming.

## Flips the arguments of a function
func flip f x y = f y x
export flip

test "flip" {
    assert flip (:) 1 2 == 2:1
}

## The identity function
func id x = x
export id

test "id" {
    assert id 3 == 3
    let x = []
    assert id x === x
}

## Call a procedure with a single argument
func call pr x = pr!(x)
export call

test "call" {
    let pr = do(x) { return x * 2 }
    assert call pr 4 == 8
}

## Defers execution of a function by wrapping its evaluation in a nullary procedure.
func thunkify f x = do() f x
export thunkify

test "thunkify" {
    let explode = fn x. exit x
    thunkify explode 3
    assert (thunkify id 5)!() == 5
}
