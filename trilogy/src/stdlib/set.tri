import "trilogy:core" as core

export contains, union, intersection, difference, symmetric_difference, push, collect

proc push!(set and typeof 'set, val) {
  core::push!(set, val)
}

test "set push" {
  let mut set = [||]
  push!(set, 5)
  assert set == [|5|]
  push!(set, 6)
  assert set == [|5, 6|]
  push!(set, 6)
  assert set == [|5, 6|]
}

func union a b = [| ..a, ..b |]

test "set union" {
  assert union [| 1, 2 |] [| 3, 2 |] == [| 1, 2, 3 |]
}

func intersection a b = [| x for x in a and is contains x b |]

test "set intersection" {
  assert intersection [| 1, 2 |] [| 3, 2 |] == [| 2 |]
}

func difference a b = [| x for x in a and is !(contains x b) |]

test "set difference" {
  assert difference [| 1, 2 |] [| 3, 2 |] == [| 1 |]
}

func symmetric_difference a b = [| x for x in union a b and is !(contains x b) || !(contains x a) |]

test "set symmetric_difference" {
  assert symmetric_difference [| 1, 2 |] [| 3, 2 |] == [| 1, 3 |]
}

func contains val set and typeof 'set = core::contains_key val set

test "set contains" {
  let set = [|1, 2, 3|]
  assert contains 3 set
  assert contains 1 set
  assert !(contains 4 set)
}

func collect iterator =
  let set = [||],
  with { iterator!(); set } {
    when 'next(val) then {
      push!(set, val)
      become unit
    }
  }

test "set collect" {
  let iterator = do() {
    yield 'next(1)
    yield 'next(2)
    yield 'next(3)
    yield 'next(2)
    yield 'next(1)
  }
  assert collect iterator == [| 1, 2, 3 |]
}
