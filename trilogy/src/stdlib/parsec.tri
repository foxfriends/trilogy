import "trilogy:debug" use dbg
import "trilogy:array" as array
import "trilogy:str" as str

func apply parser = parser!()
export apply

func char c = do() {
  if c == yield 'next {
    yield 'accept
    return c
  } else {
    yield 'reject
  }
}
export char

proc eof!() {
  if yield 'eof {
    yield 'accept
  } else {
    yield 'reject
  }
}
export eof

func seq parsers = do() array::map apply parsers
export seq

func string s = s
  |> str::chars
  |> array::map char
  |> seq
  |> map (str::join "")
export string

func map f parser = do() {
  return f parser!()
}
export map

func parse parser string =
  let mut pos = 0,
  with parser!()
    when 'eof if pos == str::length string resume true
    when 'eof resume false
    when 'next resume string.pos
    when 'accept then {
      pos += 1
      become unit
    }
    when 'reject cancel yield 'reject
    else yield
export parse
