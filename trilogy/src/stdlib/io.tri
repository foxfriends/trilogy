import "trilogy:core" as core
import "trilogy:string" as string

export print
proc print!(value and typeof 'string) {
  let err = core::print!(value)
  if err < 0 {
    yield 'ioerror
  }
}

export println
proc println!(value and typeof 'string) {
  let err = core::print!("${value}\n")
  if err < 0 {
    yield 'ioerror
  }
}

export readchar
proc readchar!() {
  let char = core::readchar!()
  return match char
    case 'eof then yield 'eof
    case typeof 'char then char
    case typeof 'number then yield 'errno(char)
    else then yield 'error
}

export readline
proc readline!() {
  let line = core::readline!()
  return match line
    case 'eof then yield 'eof
    case typeof 'string then line
    case typeof 'number then yield 'errno(line)
    else then yield 'error
}

export readlines
proc readlines!() {
  while true {
    let line = with readline!()
      when 'eof cancel break unit
      else yield
    yield 'next(line)
  }
}

export readall
proc readall!() {
  return string::collect <| readlines!()
}
