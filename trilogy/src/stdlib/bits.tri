import "trilogy:array" as array
import "trilogy:core" as core
import "trilogy:number" as number

type c {
  extern "c" proc to_bits!(val)
  export to_bits

  extern "c" proc pop_count!(bits)
  export pop_count
}

export init_zero, collect, from_array, set_at, unset_at, pop_count, concat, length, from

func length b and typeof 'bits = core::length b

func init_zero l = 0bb <~~ l

test "bits init_zero" {
  assert init_zero 0 == 0bb
  assert init_zero 3 == 0bb000
}

func from_array [] = 0bb
func from_array [true, ..rest] = 0bb1 | (from_array rest ~~> 1)
func from_array [false, ..rest] = from_array rest ~~> 1

test "bits from_array" {
  assert from_array [true, false, false, true, false] == 0bb10010
  assert from_array [] == 0bb
}

func collect iterator = from_array <| array::collect iterator

test "bits collect" {
  let iterator = do() {
    yield 'next(true)
    yield 'next(false)
    yield 'next(false)
    yield 'next(true)
    yield 'next(false)
  }
  assert collect iterator == 0bb10010
}

func set_at index bb = bb | ((bb ^ bb | 0bb1) ~> index)

test "bits set_at" {
  assert set_at 3 0bb00000 == 0bb00010
  assert set_at 3 0bb11111 == 0bb11111
  assert set_at 0 0bb00000 == 0bb10000
  assert set_at 5 0bb00000 == 0bb00000
}

func unset_at index bb = bb & ~((bb ^ bb | 0bb1) ~> index)

test "bits unset_at" {
  assert unset_at 3 0bb11111 == 0bb11101
  assert unset_at 3 0bb00000 == 0bb00000
  assert unset_at 0 0bb11111 == 0bb01111
  assert unset_at 5 0bb11111 == 0bb11111
}

func unset_at index bb = bb & ~((bb ^ bb | 0bb1) ~> index)

test "bits unset_at" {
  assert unset_at 3 0bb11111 == 0bb11101
  assert unset_at 3 0bb00000 == 0bb00000
  assert unset_at 0 0bb11111 == 0bb01111
  assert unset_at 5 0bb11111 == 0bb11111
}

func pop_count b and typeof 'bits = c::pop_count!(b)

test "bits pop_count" {
  assert pop_count 0bb00000000 == 0
  assert pop_count 0bb10000000 == 1
  assert pop_count 0bb10000001 == 2
  assert pop_count 0bb11111111 == 8
  assert pop_count 0bb11111111111111111111 == 20
}

func concat lhs rhs = lhs | (rhs ~~> length lhs)

test "bits concat" {
  assert concat 0bb0000 0bb0000 == 0bb00000000
  assert concat 0bb1 0bb0 == 0bb10
  assert concat 0bb0001 0bb1000 == 0bb00011000
  assert concat 0bb0001 0bb10000000 == 0bb000110000000
}

func from value and typeof 'number = value |> number::numer |> number::abs |> c::to_bits

test "bits from" {
  assert from 0 == 0bb00000000000000000000000000000000
  assert from 0xFFFFFFFF == 0bb11111111111111111111111111111111
  assert from 0xDEADBEEF == 0bxDEADBEEF
  assert from 0x12345678DEADBEEF == 0bx12345678DEADBEEF
}
