import "trilogy:core" as core

export from_digit, abs, max, min, re, im, conj, swap

func re num and typeof 'number = core::re!(num)

test "number re" {
  assert re 3i1 == 3
  assert re (-3i1) == -3
}

func im num and typeof 'number = core::im!(num)

test "number im" {
  assert im 3i1 == 0i1
  assert im (-3i1) == -0i1
}

func conj num and typeof 'number = re num - im num

test "number conj" {
  assert conj 3i1 == 3 - 0i1
  assert conj (-3i1) == -3 + 0i1
}

func swap num and typeof 'number = conj num * 0i1

test "number swap" {
  assert swap 3i1 == 1i3
  assert swap (-3i1) == -1i3
}

func from_digit '9' = 9
func from_digit '8' = 8
func from_digit '7' = 7
func from_digit '6' = 6
func from_digit '5' = 5
func from_digit '4' = 4
func from_digit '3' = 3
func from_digit '2' = 2
func from_digit '1' = 1
func from_digit '0' = 0

func abs n = if n >= 0 then n else -n

test "number abs" {
  assert abs (-3) == 3
  assert abs 3 == 3
  assert abs 0 == 0
}

func max a b = if a > b then a else b

test "number max" {
  assert max 1 5 == 5
  assert max 5 1 == 5
  assert max (-1) (-5) == -1
}

func min a b = if a < b then a else b

test "number min" {
  assert min 1 5 == 1
  assert min 5 1 == 1
  assert min 1 (-5) == -5
}
