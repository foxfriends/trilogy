import "trilogy:core" as core
import "trilogy:string" use replace_all

export to_string, dbg, trace, print_backtrace

func escape_string string = replace_all "\"" "\\\"" string

func to_string val =
  match val {
    case lhs:rhs then "(${to_string lhs}:${to_string rhs})"
    case typeof 'atom then "'${core::to_string val}"
    case typeof 'struct {
      let lhs:rhs = core::destruct val
      "${to_string lhs}(${to_string rhs})"
    }
    case typeof 'array {
      let mut str = "["
      let mut i = 0
      while i < core::length val {
        str <>= "${to_string val.i}"
        i += 1
        if i != core::length val {
          str <>= ", "
        }
      }
      str <> "]"
    }
    case typeof 'set {
      let mut str = "[|"
      let mut i = 0
      let arr = core::to_array val
      while i < core::length arr {
        str <>= "${to_string arr.i}"
        i += 1
        if i != core::length arr {
          str <>= ", "
        }
      }
      str <> "|]"
    }
    case typeof 'record {
      let mut str = "{|"
      let mut i = 0
      let arr = core::to_array val
      while i < core::length arr {
        str <>= "${to_string arr.i.'left} => ${to_string arr.i.'right}"
        i += 1
        if i != core::length arr {
          str <>= ", "
        }
      }
      str <> "|}"
    }
    case typeof 'string then "\"${escape_string val}\""
    case typeof 'bits then "0bb${core::to_string val}"
    else core::to_string val
  }

proc dbg!(value) {
  core::print!("${to_string value}\n")
  return value
}

func trace label value = {
  core::print!("${to_string label}: ${to_string value}\n")
  value
}

proc print_backtrace!() {
  # Don't include "print_backtrace" in the printed backtrace, it's just annoying
  let [_, ..backtrace] = core::backtrace!()
  for frame in backtrace {
    let {| "path" => path, "name" => name, "span" => (line:col):_, .._ |} = frame
    core::print!("${name}\n\tat ${path} +${line}:${col}\n")
  }
}
